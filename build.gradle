static def isSnapshot(version) {
    return version.contains("SNAPSHOT")
}

static def getRepositoryUrl(version) {
    if (isSnapshot(version)) {
        return 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = '1.17'
    targetCompatibility = '1.17'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'com.google.guava:guava:31.1-jre'

        compileOnly 'com.github.spotbugs:spotbugs-annotations:4.7.3'
    }

    group = 'io.contek.tinker'
    version = '2.2.2'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar) {
        from javadoc
        javadoc.options.addStringOption('Xdoclint:none', '-quiet')
        archiveClassifier.set('javadoc')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = "tinker-${project.name}"
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    afterEvaluate {
                        name = project.description
                        description = project.description
                    }

                    url = 'https://github.com/contek-io/io.contek.tinker'

                    scm {
                        connection = 'scm:git:git://github.com/contek-io/io.contek.tinker.git'
                        developerConnection = 'scm:git:ssh://github.com/contek-io/io.contek.tinker.git'
                        url = 'http://github.com/contek-io/io.contek.tinker/tree/master'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'contek'
                            name = 'Dev Contek'
                            email = 'dev@contek.io'
                            organization = 'Contek'
                            organizationUrl = 'https://contek.io'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = getRepositoryUrl(version)
                credentials {
                    username = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ''
                    password = project.hasProperty('sonatypePassword') ? sonatypePassword : ''
                }
            }
        }
    }

    signing {
        required { !isSnapshot(version) }
        sign publishing.publications.mavenJava
    }
}
